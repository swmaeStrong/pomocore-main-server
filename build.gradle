plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id "io.sentry.jvm.gradle" version "5.8.0"
}

group = 'com.swmStrong'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	runtimeOnly 'com.mysql:mysql-connector-j'
	implementation 'org.postgresql:postgresql:42.7.3'
	runtimeOnly 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'me.paulschwarz:spring-dotenv:3.0.0'
	implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'

	// caffeine
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

	// guava
	implementation 'com.google.guava:guava:33.0.0-jre'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2'

	// actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// AWS
	implementation 'com.amazonaws:aws-java-sdk-ses:1.12.664'
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.0.1'

	// Sentry
	implementation 'io.sentry:sentry-spring-boot-starter-jakarta:7.19.1'
	implementation 'io.sentry:sentry-logback:7.19.1'

	//Aho-corasick
	implementation 'com.hankcs:aho-corasick-double-array-trie:1.2.2'

	// JSON structured logging
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('updateSubmodule', Exec) {
	description = '서브 모듈을 최신화 합니다.'
	workingDir = file('submodule')
	commandLine 'git', 'pull', '--ff-only'
}

tasks.register('copyYML', Copy) {
	description = '서브 모듈의 yml 파일을 resources 디렉토리로 복사합니다.'
	dependsOn tasks.named('updateSubmodule')

	from(file('submodule')) {
		include '*.yml'
	}
	into(file('src/main/resources'))
}

tasks.named('processResources') {
	dependsOn tasks.named('copyYML')
}